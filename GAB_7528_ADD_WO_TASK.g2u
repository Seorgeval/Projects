Program.Sub.Preflight.Start
Program.External.Include.Library("6008.lib")
Program.External.Include.Library("500030.lib")
Program.Sub.Preflight.End

Program.Sub.Main.Start
'George Sandoval and Deni Yunus 
'24 May 2024 
'ARC 7528
'Script that will auto generate a task wo sequence, 000499, when a WO is generated from a SO
'When creating a new WO for a part that a router does not exist after clicking on the SO Line screen > Gen WO button, the customer would like to automatically add a TASK sequence to the WO to prevent their estimators from manually adding it to every new router.
V.Local.sDate.Declare
v.Local.sStartDate.Declare
v.Local.sEndDate.Declare
V.Local.sSchedD.Declare
V.Local.sFileName.Declare
v.Local.bFileExists.Declare
V.Local.sFile.Declare
v.Local.sWC.Declare
V.Local.sQuery.Declare
V.Local.sOrderNo.Declare
V.Local.sOrderNoRight.Declare
V.Local.sOrderLine.Declare
V.Local.sReturn.Declare
V.Local.sSQL.Declare
V.Local.sWOFormatted.Declare

F.ODBC.Connection!conx.OpenCompanyConnection

'Hook 12340 - Order Entry > Lines > Gen WO button
F.Intrinsic.Control.If(V.Caller.Hook,=,12340)
	'Create a temp file to identify whether the user generates the WO from from SO > Line > Gen WO
	F.Intrinsic.String.Format(V.Ambient.Date,"YYYYMMDD",V.Local.sDate)
	'000060 - SO Number
	'000051 - SO Line
	F.Intrinsic.String.LPad(V.Passed.000060, "0", 6, V.Local.sOrderNo)
	F.Intrinsic.String.LPad(V.Passed.000051, "0", 3, V.Local.sOrderLine)
	F.Intrinsic.String.Right(V.Local.sOrderNo, 4, V.Local.sOrderNoRight)
	F.Intrinsic.String.Build("{0}\GSS\INA_{1}_{2}_{3}_ADD_TASK.txt", V.System.Temp, V.Caller.User, V.Local.sOrderNoRight, V.Local.sDate, V.Local.sFileName)
	F.Intrinsic.File.Exists(V.Local.sFileName,v.Local.bFileExists)
	f.Intrinsic.Control.If(v.Local.bFileExists)
		f.Intrinsic.File.DeleteFile(v.Local.sFileName)
	f.Intrinsic.Control.EndIf
	V.Local.sFile.Set("Y")
	F.Intrinsic.File.String2File(V.Local.sFileName,V.Local.sFile)
F.Intrinsic.Control.ElseIf(V.Caller.Hook,=,16800)
'Hook 16800 - WO Post Ok hook (On the scheduling screen after scheduling)
	F.Intrinsic.String.Format(V.Ambient.Date,"YYYYMMDD",V.Local.sDate)
	
	'Need order number + order line for this job + suffix
	'000900 - WO Number
	'000901 - WO Suffix
	'Need to follow the Global Shop core options for numbering the WO to find the WO number tied to the SO. SO number 0080368 will be WO number 036811 for line 1. Ignore the suffix since we have the date. So match the right 4 of the SO to the Left 4 of the WO
	
	V.Local.sWOFormatted.Set(V.Passed.009000)
	F.Intrinsic.String.Left(V.Local.sWOFormatted, 4, V.Local.sWOFormatted)
	F.Intrinsic.String.Build("{0}\GSS\INA_{1}_{2}_{3}_ADD_TASK.txt", V.System.Temp, V.Caller.User, V.Local.sWOFormatted, V.Local.sDate, V.Local.sFileName)
	F.Intrinsic.File.Exists(V.Local.sFileName,v.Local.bFileExists)
	f.Intrinsic.Control.If(v.Local.bFileExists)
	
		'Check PO/WO Note Option
		f.ODBC.Connection!conx.ExecuteAndReturn("SELECT TEXT1 FROM OP_HEADER WHERE ID='401238' AND SEQUENCE='0000'",v.Local.sWC)
		f.Intrinsic.Control.If(v.Local.sWC.trim,=,"")
			F.Intrinsic.UI.Msgbox("Task Workcenter is not setup yet on System Support > Admin > Company Option (Advanced) > Manufacturing > Task Work Center. Process is canceled","Add WO Task")
		f.Intrinsic.Control.EndIf
		f.Data.DataTable.AddRow("6008","WONo",Variable.Passed.009000,"Suffix",Variable.Passed.009001,"Seq","000499","OpDesc","TASK","LMO","T","PWC",v.Local.sWC,"EstHrs",1,"SignoffUser","TSK-PROG")
	
		'Upload Job Cost Sequence Lines to Router Header Master, UPLJOBST
		f.Intrinsic.Control.CallSub(6008Sync)
		
		v.Local.sSchedD.Set("F")
		
		F.Intrinsic.String.Build("SELECT top 1 DATE_START, DATE_DUE FROM V_JOB_HEADER WHERE JOB = '{0}' AND SUFFIX = '{1}'", Variable.Passed.009000, Variable.Passed.009001, V.Local.sQuery)
		F.Data.DataTable.CreateFromSQL("dtDates", "conx", V.Local.sQuery)
		F.Intrinsic.Control.If(V.DataTable.dtDates.RowCount, >, 0)
			F.Intrinsic.String.Format(V.DataTable.dtDates(0).Date_Start!FieldVal,"MMDDYY",v.Local.sStartDate)
			F.Intrinsic.String.Format(V.DataTable.dtDates(0).Date_Due!FieldVal,"MMDDYY",v.Local.sEndDate)
		F.Intrinsic.Control.EndIf

		f.Data.DataTable.AddRow("500030","StartDate",v.Local.sStartDate,"DueDate",v.Local.sEndDate,"WONum",Variable.Passed.009000,"Suffix",Variable.Passed.009001,"Schedule",V.Local.sSchedD)
		
		f.Intrinsic.Control.CallSub(500030Sync)
		f.Intrinsic.File.DeleteFile(v.Local.sFileName)
	f.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf

f.ODBC.Connection!conx.Close
f.Intrinsic.Control.End

Program.Sub.Main.End

Program.Sub.Comments.Start
${$5$}$20.1.8649.24509$}$1
${$6$}$gsandoval$}$20240814104855860$}$pxyipsmdqasyAY7lJ5+YqAibKN4I37/YXQ2byAA0xDE8V5a48+RAYAJSN/MEjqSfweDm7Puz8OqMTkNQtvS5Ag==
Program.Sub.Comments.End